dnl Process with autoconf to generate configure script   -*- sh -*-

AC_INIT(xforms,1.1cvs,xforms@bob.usuhs.mil)
AC_PREREQ(2.52)
AC_CONFIG_SRCDIR(lib/forms.c)
AM_CONFIG_HEADER([lib/config.h])

AC_CONFIG_AUX_DIR(config)

PACKAGE=xforms
XFORMS_SET_VERSION(1,0,2)
XFORMS_CHECK_VERSION

AM_MAINTAINER_MODE
AC_CANONICAL_TARGET

AM_INIT_AUTOMAKE(xforms, $VERSION)

# fix the value of the prefixes.
test "x$prefix" = xNONE && prefix=$ac_default_prefix
test "x$exec_prefix" = xNONE && exec_prefix='${prefix}'

### Set the execute permissions of the various scripts correctly
for file in config/install-sh config/mkinstalldirs ; do
  chmod 755 ${srcdir}/${file}
done

### Check for programs
XFORMS_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_RANLIB

### check for special systems
AC_ISC_POSIX
AC_AIX

### Check for some Cygwin-specific details.
CHECK_WITH_CYGWIN

# Work around a problem in automake 1.4: when invoking install-strip,
# INSTALL_PROGRAM is changed to 'install -s', and since
# INSTALL_SCRIPT==INSTALL_PROGRAM, we get errors with fileutils-4.0
# which returns an error condition when stripping fails.
INSTALL_SCRIPT='${INSTALL}'

### Setup libtool
AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL

### Add extra directories to check for libraries.
XFORMS_WITH_DIR([extra-lib],[extra library directory],extra_lib, NONE)
XFORMS_LOOP_DIR($xforms_cv_extra_lib,XFORMS_ADD_LIB_DIR(xforms_ldflags,$dir))
test ! x"$xforms_ldflags" = x && LDFLAGS="$xforms_ldflags $LDFLAGS"

### Add extra directories to check for include files.
XFORMS_WITH_DIR([extra-inc],[extra include directory],extra_inc, NONE)
XFORMS_LOOP_DIR($xforms_cv_extra_inc,XFORMS_ADD_INC_DIR(xforms_cppflags,$dir))
test ! x"$xforms_cppflags" = x && CPPFLAGS="$xforms_cppflags $CPPFLAGS"

### Add both includes and libraries
XFORMS_WITH_DIR([extra-prefix],[extra lib+include directory],extra_prefix, NONE, ${prefix})
XFORMS_LOOP_DIR($xforms_cv_extra_prefix,[
XFORMS_ADD_INC_DIR(CPPFLAGS,$dir/include)
  XFORMS_ADD_LIB_DIR(LDFLAGS,$dir/lib)])

# Check for X
AC_PATH_XTRA

# Checks for libraries.
dnl by testing these we check if it is ok to have
dnl -lc and -lm as args to the compiler
AC_CHECK_LIB(m, sin)
AC_CHECK_LIB(c, fopen)
XFORMS_CHECK_LIB_JPEG

# Checks for header files.
XFORMS_PATH_XPM
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS([fcntl.h float.h limits.h locale.h malloc.h memory.h stdlib.h string.h sys/select.h sys/time.h sys/timeb.h sys/types.h unistd.h, GL/glx.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_HEADER_STAT
AC_C_CONST
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM

# Checks for library functions.
AC_FUNC_FORK
AC_FUNC_MALLOC
AC_TYPE_SIGNAL
AC_FUNC_STAT
AC_FUNC_STRTOD
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([dup2 endpwent floor ftime getcwd gethostname gettimeofday getwd memmove memset modf pow putenv select setlocale strcasecmp strchr strdup strerror strrchr strstr strtol])

AC_CHECK_FUNCS(snprintf vsnprintf)
XFORMS_CHECK_DECL(snprintf, stdio.h)
XFORMS_CHECK_DECL(vsnprintf, stdio.h)

### Some information on what just happened
VERSION_INFO="Configuration\n\
  Host type:                      ${host}\n\
  Special build flags:            ${xforms_flags}\n\
  C Compiler:                     ${CC}\n\
  C Compiler flags:               ${CFLAGS}\n\
  Linker flags:                   ${LDFLAGS}\n"


### Not used for now.
### AC_SUBST(VERSION_INFO)

AC_SUBST(X_LIBS)
AC_SUBST(X_PRE_LIBS)

## Some config.h stuff

AH_TOP([
/*
 * This file is part of the XForms library package.
 *
 * XForms is free software; you can redistribute it and/or modify it
 * under the terms of the GNU Lesser General Public License as
 * published by the Free Software Foundation; either version 2.1, or
 * (at your option) any later version.
 *
 * XForms is distributed in the hope that it will be useful, but
 * WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with XForms; see the file COPYING.  If not, write to
 * the Free Software Foundation, 59 Temple Place - Suite 330, Boston,
 * MA 02111-1307, USA.
 */

/* \file config.h
 *
 * This is the compilation configuration file for xforms.
 * It was generated by autoconfs configure.
 * You might want to change some of the defaults if something goes wrong
 * during the compilation.
 */

#ifndef CONFIG_H
#define CONFIG_H
])


AH_BOTTOM([

#endif /* NOT CONFIG_H */
])

### Finish the work.
AC_CONFIG_FILES([Makefile \
       xforms.spec \
       lib/include/AAA.h \
       config/Makefile \
       lib/Makefile \
       lib/bitmaps/Makefile \
       lib/fd/Makefile \
       lib/include/Makefile \
       lib/OS2/Makefile \
       lib/private/Makefile \
       image/Makefile
       gl/Makefile \
       fd2ps/Makefile \
       fd2ps/test/Makefile \
       fdesign/Makefile \
       fdesign/fd/Makefile \
       fdesign/fd4test/Makefile \
       fdesign/notes/Makefile \
       fdesign/spec/Makefile \
       fdesign/xpm/Makefile \
       demos/Makefile \
       demos/fd/Makefile
])

AC_OUTPUT

# show version information
echo
printf "$VERSION_INFO"
echo

# Display a final warning if there has been a XFORMS_ERROR
XFORMS_CHECK_ERRORS
